description = """Embers - Embedded Sql Reporting.
Embeddable servlets that expose a restricted set of queries for an sql database.
Hosted on github: https://github.com/alfanse/embers.git"""

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url 'http://repo.bodar.com/' }
    }

    ext {
        appName = "Embers"
    }

    group = 'adf.embers'
    version = System.getProperty('build.number', 'DEV-SNAPSHOT')
}

task codeCoverageReport(type: JacocoReport) {
//swiped from http://csiebler.github.io/blog/2014/02/09/multi-project-code-coverage-using-gradle-and-jacoco/

    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        html.enabled true
        html.destination "${buildDir}/reports/jacoco"
    }
}

// always run the tests before generating the report
codeCoverageReport.dependsOn {
    subprojects*.test
}

check.dependsOn jacocoTestReport


subprojects {

    extensions.add("libs", [
            core: [
//            the only libraries the production jar depends on
                'javax.ws.rs:javax.ws.rs-api:2.1',
                'javax.inject:javax.inject:1',
                'org.jdbi:jdbi:2.78',
                'javax.xml.bind:jaxb-api:2.3.0',
                'com.sun.xml.bind:jaxb-core:2.3.0',
                'com.sun.xml.bind:jaxb-impl:2.3.0'
            ],
            unit_tests: [
                'junit:junit:4.12',
                'org.easytesting:fest-assert-core:2.0M10',
                'org.mockito:mockito-core:2.15.0'
            ],
            acceptance_tests: [
//            the bdd framework, makes pretty html reports from Junit tests
                'com.googlecode.yatspec:yatspec:217'
            ],
            jersey: [
//            implements javax.ws.rs
                'org.glassfish.jersey.core:jersey-server:2.2',
                'org.glassfish.jersey.containers:jersey-container-servlet:2.2'
            ],
            jaxson: [
//              handles json serialisation
                'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.5.2'
            ],
            gson: [
                //serialise java to json
                'com.google.code.gson:gson:2.8.2'
            ],
            json_assert: [
                'org.skyscreamer:jsonassert:1.2.3'
            ],
            jetty: [
//              beloved server
                'org.eclipse.jetty:jetty-server:9.3.0.M1',
                'org.eclipse.jetty:jetty-servlet:9.3.0.M1'
            ],
            database: [
//              beloved in-memory DB for testing.
                'org.hsqldb:hsqldb:2.3.2'
            ]
    ]);
}

task wrapper(type: Wrapper) {
    //to pick up a new version execute: ./gradlew wrapper
    gradleVersion = '4.6'
}
