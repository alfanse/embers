description = """Embers - Embedded Sql Reporting.
Embeddable servlets that expose a restricted set of queries for an sql database.
Hosted on github: https://github.com/alfanse/embers.git"""

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.adarshr:gradle-test-logger-plugin:1.7.0"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'com.adarshr.test-logger'

//    sourceCompatibility = 17

    repositories {
        mavenCentral()
        mavenLocal()
    }

    ext {
        appName = "Embers"
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    group = 'adf.embers'
    version = System.getProperty('build.number', 'DEV-SNAPSHOT')
}

task codeCoverageReport(type: JacocoReport) {
//swiped from http://csiebler.github.io/blog/2014/02/09/multi-project-code-coverage-using-gradle-and-jacoco/

    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        html.enabled true
        html.destination file("${buildDir}/reports/jacoco")
        println "code coverage reports: " + html.destination
    }
}

// always run the tests before generating the report
codeCoverageReport.dependsOn {
    subprojects*.test
}

check.dependsOn jacocoTestReport

subprojects {

    def jersey = '3.0.3'
    def jetty = '11.0.7'
    def junitJupiter = '5.7.1'

    extensions.add("libs", [
            core: [
                //the only libraries the production jar depends on
                'jakarta.ws.rs:jakarta.ws.rs-api:3.0.0',
                'jakarta.inject:jakarta.inject-api:2.0.1',
                'org.jdbi:jdbi:2.78'
            ],
            jersey: [//implements jakarta.ws.rs
                "org.glassfish.jersey.media:jersey-media-json-jackson:$jersey",
                "org.glassfish.jersey.core:jersey-server:$jersey",
                "org.glassfish.jersey.containers:jersey-container-servlet:$jersey",
                "org.glassfish.jersey.inject:jersey-hk2:$jersey"
            ],
            jaxson: [// handles json serialisation
                'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.13.1'
            ],
            gson: [ // serialise java to json
                'com.google.code.gson:gson:2.8.9'
            ],
            json_assert: [
                'org.skyscreamer:jsonassert:1.2.3'
            ],
            jetty: [ // beloved server
                 "org.eclipse.jetty:jetty-server:$jetty",
                 "org.eclipse.jetty:jetty-annotations:$jetty",
                 "org.eclipse.jetty:jetty-servlet:$jetty",
//                 "org.glassfish.jersey.containers:jersey-container-jetty-http:$jersey",
                 "org.glassfish.jersey.containers:jersey-container-servlet:$jersey",
                 "org.glassfish.jersey.inject:jersey-hk2:$jersey",
                 "org.glassfish.jersey.media:jersey-media-json-jackson:$jersey",
                 'jakarta.servlet:jakarta.servlet-api:5.0.0'
            ],
            logs: [
                'org.slf4j:slf4j-api:1.7.35',
                'org.slf4j:slf4j-simple:1.7.35'
            ],
            database: [
//              beloved in-memory DB for testing.
                'org.hsqldb:hsqldb:2.3.2'
            ],
            unit_tests: [
                "org.junit.jupiter:junit-jupiter:$junitJupiter",
                'org.easytesting:fest-assert-core:2.0M10',
                'org.mockito:mockito-core:3.8.0',
                'org.mockito:mockito-junit-jupiter:3.8.0'
            ],
            acceptance_tests: [ //the bdd framework, adds diagrams to html reports from Junit tests
                'com.github.nickmcdowall:yatspec:2021.1.1'
            ]
    ])
}
