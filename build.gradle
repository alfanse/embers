description = """Embers - Embedded Sql Reporting.
Embeddable servlets that expose a restricted set of queries for an sql database.
Hosted on github: https://github.com/alfanse/embers.git"""

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url 'http://repo.bodar.com/' }
    }

    ext {
        appName = "Embers"
    }

    group = 'adf.embers'
    version = System.getProperty('build.number', 'DEV-SNAPSHOT')
}

subprojects {

    extensions.add("libs", [
            core: [
                //the only libraries the production jar depends on
                'javax.ws.rs:javax.ws.rs-api:2.0.1',
                'javax.inject:javax.inject:1',
                'org.jdbi:jdbi:2.56'
            ],
            unit_tests: [
                'junit:junit:4.11',
                'org.easytesting:fest-assert-core:2.0M10',
                'org.hamcrest:hamcrest-core:1.3',
                'org.mockito:mockito-core:1.10.17'
            ],
            acceptance_tests: [
                'com.googlecode.yatspec:yatspec:217'
            ],
            jersey: [
//              implements javax.ws.rs
                'org.glassfish.jersey.core:jersey-server:2.2',
                'org.glassfish.jersey.containers:jersey-container-servlet:2.2'
            ],
            jaxson: [
//              handles json serialisation
                'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.5.2'
            ],
            gson: [
                    'com.google.code.gson:gson:2.3.1'
            ],
            jetty: [
//              beloved server
                'org.eclipse.jetty:jetty-server:9.3.0.M1',
                'org.eclipse.jetty:jetty-servlet:9.3.0.M1'
            ],
            database: [
//              beloved in-memory DB for testing.
                'org.hsqldb:hsqldb:2.3.2'
            ]
    ]);
}

task wrapper(type: Wrapper) {
    //to pick up a new version execute: ./gradlew wrapper
    gradleVersion = '2.4'
}
