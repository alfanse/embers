description = """Embers - Embedded Sql Reporting.
Embeddable servlets that expose a restricted set of queries for an sql database.
Hosted on github: https://github.com/alfanse/embers.git"""

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.adarshr:gradle-test-logger-plugin:4.0.0"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'com.adarshr.test-logger'

    sourceCompatibility = 17
    targetCompatibility = 17

    repositories {
        mavenCentral()
        mavenLocal()
    }

    ext {
        appName = "Embers"
    }

    group = 'adf.embers'
    version = System.getProperty('build.number', 'DEV-SNAPSHOT')
}

task codeCoverageReport(type: JacocoReport) {
//swiped from http://csiebler.github.io/blog/2014/02/09/multi-project-code-coverage-using-gradle-and-jacoco/

    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        html {
            setEnabled(true)
            outputLocation = file("${buildDir}/reports/jacoco")
//            println "code coverage reports: " + html.destination
        }
    }
}

// always run the tests before generating the report
codeCoverageReport.dependsOn {
    subprojects*.test
}

check.dependsOn jacocoTestReport
