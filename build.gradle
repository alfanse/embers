description = """Embers - Embedded Sql Reporting.
Embeddable servlet that exposes a restricted set of queries for an sql database.
Hosted on github: https://github.com/alfanse/embers.git"""

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url 'http://repo.bodar.com/' }
    }

    ext {
        appName = "Embers"
    }

    version = System.getProperty('build.number', 'DEV-SNAPSHOT')
    group = 'adf.embers'
}

subprojects {

    ext.libs = [
            core: [
                'javax.ws.rs:javax.ws.rs-api:2.0.1',
                'javax.inject:javax.inject:1',
                'org.jdbi:jdbi:2.56'
            ],
            unit_tests: [
                'junit:junit:4.11',
                'org.easytesting:fest-assert-core:2.0M10',
                'org.hamcrest:hamcrest-core:1.3',
                'org.mockito:mockito-core:1.10.17'
            ],
            acceptance_tests: [
                'com.googlecode.yatspec:yatspec:217'
            ],
            jersey: [
//                    implements javax.ws.rs
                'org.glassfish.jersey.core:jersey-server:2.2',
                'org.glassfish.jersey.containers:jersey-container-servlet:2.2'
            ],
            jaxson: [
//                    handles json serialisation
'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.5.2'
            ],
            jetty: [
//                    old trusted server
                'org.eclipse.jetty:jetty-server:9.3.0.M1',
                'org.eclipse.jetty:jetty-servlet:9.3.0.M1'
            ],
            database: [
                'org.hsqldb:hsqldb:2.3.2'
            ]
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}